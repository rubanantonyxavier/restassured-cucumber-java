buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.qameta.allure.gradle.allure:allure-plugin:2.9.6"
    }
}

plugins {
    id 'java'
    id 'io.qameta.allure' version '2.9.6'
}

group 'com.automation.services'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

ext {
    assuredVersion = '5.0.1'
    cucumberVersion = '7.8.1'
    assertjVersion = '3.23.1'
    jacksonVersion = '2.12.0'
    jsoupVersion = '1.14.3'
    itextpdfVersion = '4.0.2'
    pdfBoxVersion = '2.0.26'
    tikaVersion = '2.4.0'
    lombokVersion = '1.18.24'
    allureVersion = '2.19.0'
    snakeyamlVersion = '1.33'
    availableEnvs = ['qa', 'dev', 'uat']
    currentEnv = getEnv()
}

dependencies {
    implementation group: 'io.rest-assured', name: 'rest-assured', version: assuredVersion
    implementation group: 'io.cucumber', name: 'cucumber-java', version: cucumberVersion
    testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'org.jsoup', name: 'jsoup', version: jsoupVersion
    implementation group: 'com.itextpdf', name: 'html2pdf', version: itextpdfVersion
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: pdfBoxVersion
    implementation group: 'org.apache.tika', name: 'tika-core', version: tikaVersion
    implementation group: 'org.apache.tika', name: 'tika-parser-pdf-module', version: tikaVersion
    implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testImplementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    implementation group: 'io.qameta.allure', name: 'allure-cucumber7-jvm', version: allureVersion
    implementation group: 'io.qameta.allure', name: 'allure-rest-assured', version: allureVersion
    implementation group: 'org.yaml', name: 'snakeyaml', version: snakeyamlVersion
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

allure {
    version = allureVersion
}

task cucumber() {
    dependsOn assemble, testClasses
    def tags = findProperty('tags') == null
            ? ''
            : "${findProperty('tags')}"

    tags = tags.isEmpty()
            ? tags
            : "($tags) and"

    tags = "$tags${createExcludedTagsCmd("$currentEnv")}"
    println "Executing on $currentEnv with filter: $tags..."

    def threads = findProperty('threads') == null
            ? '1'
            : findProperty('threads')

    doLast {
        javaexec {
            systemProperties System.properties
            environment "ENV", currentEnv
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            //noinspection GroovyUnusedAssignment
            args = ['--plugin', 'pretty',
                    '--plugin', 'io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm',
                    '--glue', 'com.automation.services.step_definitions',
                    '--tags', tags,
                    '--threads', threads,
                    'src/test/resources/features']
        }
    }
}

@SuppressWarnings('GrMethodMayBeStatic')
private String getEnv() {
    String env = System.getenv("ENV") != null ? System.getenv("ENV") : "QA"
    return env
}

@SuppressWarnings('GrMethodMayBeStatic')
String createExcludedTagsCmd(String currentEnv) {
    return this.availableEnvs.stream()
            .filter(it -> it != currentEnv.toLowerCase())
            .reduce(" not (@ignore", (subtotal, element) ->
                    "$subtotal or @${element}_only") + ")"
}